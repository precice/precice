# structure/SConstruct
#
# SCons buildfile for "Structure0815" solver
#
######################################

import os;
import sys;


##### Determine boost root path from environment variable
#
boostRootPath = os.getenv ( 'BOOST_ROOT' )
if ( boostRootPath == None ):
   print 'ERROR: Environment variable BOOST_ROOT not defined!'
   sys.exit(1)
else:
   print 'Using environment variable BOOST_ROOT =', boostRootPath

tarchSrc = os.getenv ('TARCH_SRC')
if ( tarchSrc == None ):
   print 'ERROR: Environment variable TARCH_SRC not defined!'
   sys.exit(1)
else:
   print 'Using environment variable TARCH_SRC =', tarchSrc
   
preciceRoot = os.getenv ('PRECICE_ROOT')
if ( preciceRoot == None ):
   print 'ERROR: Environment variable PRECICE_ROOT not defined!'
   sys.exit(1)
else:
   print 'Using environment variable PRECICE_ROOT =', preciceRoot


##### Declare build variables and default values
#
cppdefines = [
   'PRECICE_USE_MPI',
   'tarch=tarchp2'
   ]   
libpath = []   
libs = [ 'precice' ]
cpppath = [
   boostRootPath,
   tarchSrc,
   preciceRoot + '/src/'
   ]   
ccflags = []
cxx = 'mpicxx'      # For systems offering mpicxx compiler

libpath.append('/usr/lib/')   
libs.append('python2.6')

##### Read command line arguments
#
buildmode = ARGUMENTS.get('build', 'debug')
if buildmode == 'debug':
   cppdefines.append('STRUCTURE_DEBUG_MODE')
   cppdefines.append('Debug')
   cppdefines.append('Asserts')
   ccflags.append('-g3')
   ccflags.append('-O0')
elif buildmode == 'release':
   ccflags.append('-O3')
else:
   print "ERROR: flag 'buildmode' must be set to either 'debug' or 'release'!"
   sys.exit(1)

#libpath.append (preciceRoot + '/build/' + buildmode + '-nopython/')
libpath.append (preciceRoot + '/build/' + buildmode + '/')


##### Setup build environment and issue builds
#
env = Environment ( 
   CPPDEFINES = cppdefines,  # defines for preprocessor (#define xyz)
   LIBPATH    = libpath,     # path to libraries used
   LIBS       = libs,        # libraries used (without prefix "lib" and suffix ".a"/".so"/...)
   CPPPATH    = cpppath,     # pathes where the preprocessor should look for files
   CCFLAGS    = ccflags,     # flags for the c/c++ compilers
   CXX = cxx                 # the c++ compiler that should be used
   )
   
env.Program ( buildmode + '/structure0815', Glob('*.cpp') )